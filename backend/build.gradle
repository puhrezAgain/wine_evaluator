import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.2.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.2.21'
	id 'org.springframework.boot' version '4.0.2'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jetbrains.kotlin.plugin.jpa' version '2.2.21'
	id "jacoco"
	id 'application'
}

group = 'com.wineevaluator'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot to create a wine list helper'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.apache.pdfbox:pdfbox:2.0.30'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'tools.jackson.module:jackson-module-kotlin'
	implementation 'com.google.cloud:google-cloud-vision:3.79.0'
    implementation 'org.springframework.boot:spring-boot-starter-web'

	runtimeOnly 'com.h2database:h2'

	testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
	testImplementation 'io.mockk:mockk:1.13.10'
	testImplementation 'com.ninja-squad:springmockk:4.0.2'
	testImplementation 'org.awaitility:awaitility:4.2.0'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'


}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict', '-Xannotation-default-target=param-property'
		jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
	}
}

allOpen {
	annotation 'jakarta.persistence.Entity'
	annotation 'jakarta.persistence.MappedSuperclass'
	annotation 'jakarta.persistence.Embeddable'
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.test {
    useJUnitPlatform()

    testLogging {
        events(
            TestLogEvent.FAILED,
            TestLogEvent.SKIPPED,
            TestLogEvent.PASSED,
        )

        exceptionFormat = TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true

        showStandardStreams = true
    }

	finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)

    reports {
        html.required = true
        xml.required = true
        csv.required = false
    }

	afterEvaluate {
        classDirectories.setFrom(
            files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                    "**/*Application*",
                    "**/*Config*",
                    "**/*Entity*",
                    "**/*Dto*",
                    "**/*Request*",
                    "**/*Response*",
					"**/*Repository*",
                    "**/*Jpa*",
                    "**/*Storage*",
                    "**/*Ocr*",
                    "**/*Parser*",
                    "**/*Mappings*",
                    "**/*Dto*",
                    "**/*Worker*",
                    "**/*TokenizedLine*",
                    "**/*Controller*",
                    "**/*Entity*",
                    "**/*\$Companion.class",
                    "**/*\$*inlined*.class",
                    "**/model/**",
                    "**/value/**",
                    "**/*Kt.class", // synthetic Kotlin files
                ])
            })
        )
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = "LINE"
                minimum = 0.6
            }
        }

        // Core domain logic must stay well tested
        rule {
            element = 'PACKAGE'
            includes = [
                'com.wineevaluator.wine.*',
                'com.wineevaluator.document.interpretation.*',
                'com.wineevaluator.analysis'
            ]
            limit {
                counter = 'LINE'
                minimum = 0.8
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification


application {
    mainClass = 'com.wineevaluator.WineEvaluatorApplicationKt'
}



